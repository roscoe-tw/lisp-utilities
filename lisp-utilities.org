#+TITLE: common lisp 實用工具
* lisp-utilities.asd
#+BEGIN_SRC lisp :tangle lisp-utilities.asd :padline no
  ;;;; -*- Mode: LISP; Syntax: ANSI-Common-Lisp; Base: 10 -*-
  ;;; tangled :lisp-utilities.org

  (in-package :cl-user)

  (in-package #:asdf-user)

  (defsystem #:lisp-utilities
    :version "0.1.0"
    :author "Colin <every.push.colin@gmail.com>"
    ;; :depends-on (:cl-ppcre :asdf)
    :components ((:file "packages")
		 (:file "lisp-utilities")
		 (:file "practical_common_lisp")))


#+END_SRC
* packages.lisp
#+BEGIN_SRC lisp :tangle packages.lisp
  ;;; tangled : lisp-utilities.org

  (in-package :cl-user)

  (defpackage :lisp-utilities
    (:use :common-lisp)
    (:export :last1
	     :single
	     :append1
	     :conc1
	     :mklist
	     :filter
	     :group
	     :flatten
	     :prune
	     :find2
	     :before
	     :after
	     :duplicate
	     :split-if
	     :most
	     :best
	     :mostn
	     :mapa-b
	     :map0-1
	     :map0-n
	     :map1-n
	     :map->
	     :mappend
	     :mapacars
	     :rmapcar
	     :readlist
	     :prompt
	     :break-loop
	     :mkstr
	     :symb
	     :reread
	     :explode
	     :with-gensyms))
	   

#+END_SRC
* 實用函數
  抄錄於 《On lisp》 第四章
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp
;;;; tangled : lisp-utilities.org
;;;; 抄錄於 《On lisp》 第四章

(in-package :lisp-utilities)

#+END_SRC
** 操作列表的小函數
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp
;;; 操作列表的小函數
(proclaim '(inline last1 single append1 conc1 mklist))

(defun last1 (lst)
  (car (last lst)))

(defun single (lst)
  (and (consp lst) (not (cdr lst))))

(defun append1 (lst obj)
  (append lst (list obj)))

(defun conc1 (lst obj)
  (nconc lst (list obj)))

(defun mklist (obj)
  (if (listp obj) obj (list obj)))
#+END_SRC
** 操作列表的較大函數
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp 
;;; 操作列表的較大函數
(defun longer (x y)
  (labels ((compare (x y)
	     (and (consp x)
		  (or (null y)
		      (compare (cdr x) (cdr y))))))
    (if (and (listp x) (listp y))
	(compare x y)
	(> (length x) (length y)))))

(defun filter (fn lst)
  (let ((acc nil))
    (dolist (x lst)
      (let ((val (funcall fn x)))
	(if val (push val acc))))
    (nreverse acc)))

(defun group (source n)
  (if (zerop n) (error "zero length"))
  (labels ((rec (source acc)
	     (let ((rest (nthcdr n source)))
	       (if (consp rest)
		   (rec rest (cons (subseq source 0 n) acc))
		   (nreverse (cons source acc))))))
    (if source (rec source nil) nil)))

#+END_SRC
** 使用雙遞歸的列表實用工具
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp 
;;; 使用雙遞歸的列表實用工具

(defun flatten (x)
  (labels ((rec (x acc)
	     (cond ((null x) acc)
		   ((atom x) (cons x acc))
		   (t (rec (car x) (rec (cdr x) acc))))))
    (rec x nil)))

(defun prune (test tree)
  (labels ((rec (tree acc)
	     (cond ((null tree) (nreverse acc))
		   ((consp (car tree))
		    (rec (cdr tree)
			 (cons (rec (car tree) nil) acc)))
		   (t (rec (cdr tree)
			   (if (funcall test (car tree))
			       acc
			       (cons (car tree) acc)))))))
    (rec tree nil)))

#+END_SRC
** 搜索列表的函數
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp
;;; 搜索列表的函數
(defun find2 (fn lst)
  (if (null lst)
      nil
      (let ((val (funcall fn (car lst))))
	(if val
	    (values (car lst) val)
	    (find2 fn (cdr lst))))))

(defun before (x y lst &key (test #'eql))
  (and lst
       (let ((first (car lst)))
	 (cond ((funcall test y first) nil)
	       ((funcall test x first) lst)
	       (t (before x y (cdr lst) :test test))))))

(defun after (x y lst &key (test #'eql))
  (let ((rest (before y x lst :test test)))
    (and rest (member x rest :test test))))

(defun duplicate (obj lst &key (test #'eql))
  (member obj (cdr (member obj lst :test test))
	  :test test))

(defun split-if (fn lst)
  (let ((acc nil))
    (do ((src lst (cdr src)))
	((or (null src) (funcall fn (car src)))
	 (values (nreverse acc) src))
      (push (car src) acc))))

#+END_SRC
** 帶有元素比較的搜索函數
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp
;;; 帶有元素比較的搜索函數

(defun most (fn lst)
  (if (null lst)
      (values nil nil)
      (let* ((wins (car lst))
	     (max (funcall fn wins)))
	(dolist (obj (cdr lst))
	  (let ((score (funcall fn obj)))
	    (when (> score max)
	      (setq wins obj
		    max score))))
	(values wins max))))

(defun best (fn lst)
  (if (null lst)
      nil
      (let ((wins (car lst)))
	(dolist (obj (cdr lst))
	  (if (funcall fn obj wins)
	      (setq wins obj)))
	wins)))

(defun mostn (fn lst)
  (if (null lst)
      (values nil nil)
      (let ((result (list (car lst)))
	    (max (funcall fn (car lst))))
	(dolist (obj (cdr lst))
	  (let ((score (funcall fn obj)))
	    (cond ((> score max)
		   (setq max score
			 result (list obj)))
		  ((= score max)
		   (push obj result)))))
	(values (nreverse result) max))))

#+END_SRC
** 映射
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp
;;; 映射

(defun mapa-b (fn a b &optional (step 1))
  (do ((i a (+ i step))
       (result nil))
      ((> i b) (nreverse result))
    (push (funcall fn i) result)))

(defun map0-n (fn n)
  (mapa-b fn 0 n))

(defun map1-n (fn n)
  (mapa-b fn 1 n))

(defun map-> (fn start test-fn succ-fn)
  (do ((i start (funcall succ-fn i))
       (result nil))
      ((funcall test-fn i) (nreverse result))
    (push (funcall fn i) result)))

(defun mappend (fn &rest lsts)
  (apply #'append (apply #'mapcar fn lsts)))

(defun mapcars (fn &rest lsts)
  (let ((result nil))
    (dolist (lst lsts)
      (dolist (obj lst)
	(push (funcall fn obj) result)))
    (nreverse result)))

(defun rmapcar (fn &rest args)
  (if (some #'atom args)
      (apply fn args)
      (apply #'mapcar
	       #'(lambda (&rest args)
		   (apply #'rmapcar fn args))
		 args)))
#+END_SRC
** I/O
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp 
;;; I/O

(defun readlist (&rest args)
  (values (read-from-string
	   (concatenate 'string "("
				  (apply #'read-line args)
				  ")"))))
(defun prompt (&rest args)
  (apply #'format *query-io* args)
  (read *query-io*))

(defun break-loop (fn quit &rest args)
  (format *query-io* "Entering break-loop.'~%")
  (loop
     (let ((in (apply #'prompt args)))
       (if (funcall quit in)
	   (return)
	   (format *query-io* "~A~%" (funcall fn in))))))

#+END_SRC
** 操作符號與字符串
#+BEGIN_SRC lisp :tangle lisp-utilities.lisp 
;;; 操作符號與字符串
(defun mkstr (&rest args)
  (with-output-to-string (s)
    (dolist (a args) (princ a s))))

(defun symb (&rest args)
  (values (intern (apply #'mkstr args))))

(defun reread (&rest args)
  (values (read-from-string (apply #'mkstr args))))

(defun explode (sym)
  (map 'list #'(lambda (c)
		 (intern (make-string 1
				      :initial-element c)))
	(symbol-name sym)))

#+END_SRC
* 抄錄於《Practical Common Lisp》
** with-gensyms
#+BEGIN_SRC lisp :tangle practical_common_lisp.lisp
  ;;;tangled : lisp-utilities.org
  (in-package :lisp-utilities)

  (defmacro with-gensyms ((&rest names) &body body)
    `(let ,(loop for n in names collect `(,n (gensym)))
       ,@body))

#+END_SRC
